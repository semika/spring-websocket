plugins {
	// This configurations will be applied only to
    // parent project
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

//This will be applied only to 'web-socket-api' project
project ("web-socket-api") {
	apply plugin: 'java'
	task("hello").doLast{
		println "I am from $project.name"
	}
}

//The bellow configurations will be applied
//to all sub projects.
subprojects {
	apply plugin: 'java'

	// 'hello1' task will be applied to all sub projects
	task("hello1").doLast {
		println "I am from $project.name"
	}

	jar.enabled=true
}

//configurations which applied for all projects
allprojects {
	apply plugin: 'java'
	group = 'com.semika'
	version = '0.0.1-SNAPSHOT'
	java.sourceCompatibility(JavaVersion.VERSION_11)

	// common repositories for all projects goes here
	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		//common dependencies for all projects should go here.
		compileOnly "org.projectlombok:lombok:1.18.22"
		annotationProcessor "org.projectlombok:lombok:1.18.22"
		implementation 'com.google.guava:guava:31.0.1-jre'

		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

//The followings are the dependencies of root project only.
dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.webjars:webjars-locator-core'
	implementation 'org.webjars:sockjs-client:1.0.2'
	implementation 'org.webjars:stomp-websocket:2.3.3'
	implementation 'org.webjars:bootstrap:3.3.7'
	implementation 'org.webjars:jquery:3.1.1-1'

	implementation project(":web-socket-api")
	implementation project(":web-socket-impl")
}
